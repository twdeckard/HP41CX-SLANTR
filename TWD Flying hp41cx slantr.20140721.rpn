;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; IN-FLIGHT AVIGATION PACKAGE FOR AREA NAVIGATION BETWEEN TWO FIXES
; USES GROUND BASED VOR NAVIGATION AIDS TO UPDATE DEDUCED POSITION(S)
; CREATES A NAVIGATION LOG OF POSITION, COURSE, HEADING, DISTANCES AND  
; CLOSEST FIX EVERY 5 MINUTES
;
; COPYRIGHT 2013 TODD DECKARD.  THIS PROGRAM IS SUPPLIED WITHOUT REPRESENTATION 
; OR WARRANTY OF ANY KIND.  TODD DECKARD ASSUMES NO RESPONSIBILITY AND WILL
; HAVE NO LIABILITY, CONSEQUENTIAL OR OTHERWISE, OF ANY KIND ARISING FROM 
; THE USE OF THIS PROGRAM MATERIAL OR ANY PART THEREOF. 
;
; REQUIRES HP41CX (OR TIME MODULE AND EXTENDED MEMORY FUNCTIONS)
; REQUIRES DATABASE OF WAYPOINTS WITH LAT AND LONG IN EXTENDED MEMORY
; REQUIRES SUPPLEMENTAL FILES FOR MAGNETIC VARIATION 
;
; 
; 124 REGISTERS OF EXTENDED MEMORY AFFORDS APPROX A DOZEN WAYPOINTS
;			
; 1. BEFORE TAKEOFF NAVIGATOR ENTERS ANY ADDITIONAL WAYPOINTS IN 
;    EXTENDED MEMORY FOR THE INTENDED ROUTE OF FLIGHT
;
; 2. INITIALIZE SLANTR WHICH PROMPTS FOR DEPARTURE, DESTINATION, IAS, 
;    CURRENT BARO, PLAN ALTITUDE, FORECAST WINDS, TEMP
;    FIRST NAVIGATION LOG OUTPUT PROVIDES COURSE, DISTANCE, 
;    RECOMMENDED MAGNETIC HEADING, ANTICIPATED ETE
;
; 3. DURING BEFORE-TAKE-OFF CHECK EXECUTE HACKTO, WHICH MARKS 
;    TAKE OFF AND BEGINS REPEATING FIVE MINUTE INTERVAL UPDATE 
;    OF DED RECON POSITION
;
;    AT FIVE MINUTE INTERVALS SYSTEM WILL DISPLAY NEW NAVIGATION LOG OUTPUT
;
; 4. ENROUTE NAVIGATOR MARKS CHECKPOINT PAIRS AND DISTANCES TO UPDATE 
;    GROUND SPEED, AND TIME AND BEARING FROM KNOWN WAYPOINTS TO UPDATE POSITION
;
;    HACKC1, HACKC2 - MARKS OVERFLIGHT OF KNOWN CHECKPOINTS AND PROMPTS 
;    FOR DISTANCE, UPDATES GROUND SPEED
;
;    HACKOV - MARKS OVERFLIGHT OF KNOWN FIX AND UPDATES POSITION
;    HACKFX - MARKS DISTANCE AND BEARING FROM KNOWN FIX AND UPDATES POSITION
; 
;    VOR1B1, VOR1B2  - MARKS TWO BEARINGS FROM WAYPOINT TO UPDATE POSITION
;    VOR1B1, VOR2B1  - MARKS TWO INTERSECTING BEARINGS TO UPDATE POSITION   
;
; 5. IF DIVERTED FROM DIRECT COURSE UPDATE ASSIGNED HEADING FOR DED RECKOGNING
;
;    HEADNG - ASSIGNED HEADING
;    RESUME - RETURN TO A DIRECT HEADING TO COURSE FROM CURRENT POSITION
;    
; 6. ENDFLT - SUSPENDS DEDUCED RECKONING UPDATES	
;			
;    NOTE: WINDS ALOFT ARE USED TO PREDICT AN ANTICIPATED GROUND SPEED 
;          UNTIL AT LEAST TWO FIXES ARE DETERMINED FROM GROUND CHECKPOINTS
;
;    NOTE: CLOSEST FIX IS DISPLAYED IN BEARING TO, NAVIGATION PLOTS 
;          ARE INPUT IN BEARING FROM
;			
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
; 072114
;
; 01	SLANTR	MAIN ENTRY POINT FOR /R AREA NAVIGATION 
; 10    HACKTO  HACK TIME OF TAKEOFF; CREATE ALARM FOR MAIN LOOP EVERY 5 MIN
; 12    HACKOV  HACK TIME OVER CHECKPOINT; PROMPT FOR IDENT; UPDATE POSITION
; 	HACKFX	HACK TIME AND DISTANCE FROM CHECKPOINT
; 	HACKC1
; 	HACKC2
; 86	M%	INTERRUPT FUNCTION TO UPDATE AND DISPLAY DEDUCED POSITION 
; 12	HACKOV	HACK TIME OVER CHECKPOINT; PROMPT FOR IDENT; UPDATE POSITION
; 26	PREFLT	PROMPT FOR ORIGIN AND DESTINATION AND FLT PLAN ALTITUDE
; 96-97	ALTMTR	PROMPT FOR INDICATED ALTITUDE; KOLSMANN SETTING; TEMP C
; 98	FCSTWD	ENTER FORECAST WINDS ALOFT 
;	HEADNG
;	RESUME
; 31    CATWPT	DISPLAY WAYPOINTS
; 34	NAVLOG
; 46-55 DISPLA
; 36	*INIPOS	LOOKUP DEP&DST LAT/LON, SET FIX AND DED POSITIONS TO DEP
; 40	*CRSHDG	COMPUTE COURSE, HDNG, ETE, DISTANCES REMAINING AND FLOWN
; 74    *GCCRS
; 78    *SIGHT
; 76    *GCPLT
; 38	*DEDREC	HACK TIME OF DED RECKON POSITION; UPDATE DED RECKON LAT/LON
;       VOR1B1
; 65    VOR1B2
; 60	*CLSEST	COMPUTE CLOSEST WAYPOINT
; 70	*MAGVAR	INTERPOLATED MAGNETIC VARIATION
; 62	*DENALT
; 42    *SPDHDG
;       ACTWND
;       ENDFLT
; a 		RECIPROCAL BEARING
; b 		PROMPT "BEARING FROM?"
; c 		PROMPT "VOR ID?"
; d		PROMPT "DIST?"
; e		PROMPT "MAGHD?"
; 99		RETRIEVE TWO DATA VALUES FROM NAMED FILE (LAT/LONG OF FIX)
; 			
; 	FILES:	NAVREG	TEXT FILE OF KEY NAVIGATION VARIABLES
; 		WPTLST	TEXT FILE OF AIRPORTS AND VOR BEACONS
; 		XXX	DATA FILE FOR WAYPOINT; DECIMAL LAT/LON; 
; 		KXXX	DATA FILE FOR AIRPORT; DECIMAL LAT/LON
; 		MAGVAR  DATA FILE FOR MAGNETIC VARIATION POLYNOMIAL
; 		NAVPOS	TEXT FILE OF NAVIGATION POSITION
; 			
; 	FLAGS:	<NOTE FLAG TESTS ARE LARGELY UNIMPLEMENTED>
; 	01	FIRST BEARING RECORDED IF TRUE (VOR 1 BEARING 1)
; 	02 	NEW ALTITUDE, UPDATE DENSITY ALTITUDE, WINDS, AND AIRSPEEDS
; 	03 	NEW INDICATED AIRSPEED, UPDATE TRUE AIRSPEED AND GROUND SPEED
; 	04 	WINDS ALOFT FORECAST/OBSERVED (OBSERVED WINDS ALOFT IF TRUE)
; 	05 	GROUND SPEED EXPECTED/OBSERVED (OBSERVED GROUND SPEED IF TRUE)
; 	06 	SUSPEND TIMER INTERRUPT/DED RECON UPDATE BETWEEN FIXES
; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
;		NAVREG FILE	
;	R00	0DEPART	;0 ALPHA 
;	R01	0DEST	;1 KTS
;	R02	5TIMOFF	;2 NM
;	R03	4TRUCRS	;3 TEMPERATURE CELSIUS
;	R04	4MAGHD	;4 DEGREES 
;	R05	2DISTRM	;5 TIME IN HOURS MINUTES SECONDS
;	R06	2DISTFN	;6 LATITUDE DISPLAYED IN HOURS MINUTES SECONDS
;	R07	1GS	;7 LONGITUDE DISPLAYED IN HOURS MINUTES SECONDS
;	R08	1TAS	;8 BAROMETRIC PRESSURE (KOLLSMAN SETTING)
;	R09	1IAS	;9 FEET
;	R10	3TEMPC	
;	R11	8ALTSET	
;	R12	9ALTITD	
;	R13	4MAGVAR	
;	R14	4WINDIR	
;	R15	1WINSPD	
;	R16	9DENALT	
;	R17	6FIXLAT	
;	R18	7FIXLON	
;	R19	5TIMOFF	
;	R20	6DEDLAT	
;	R21	7DEDLON	
;	R22	5DEDTIM	
;	R23	5LASTIM	
;	R24	0LSTFIX	
;	R25	0VOR1	
;	R26	4BRNG1	
;	R27	4BRNG2	
;	R28	5TIME1	
;	R29	5TIME2	
;	R30	0VOR2	
;	R31	4BRNG3	
;	R32	2CRSTRK	; NOT IMPLEMENTED CONSIDER REPLACING WITH FUELRM
;	R33	6DEPLAT	
;	R34	6DEPLON	
;	R35	6DSTLAT	
;	R36	6DSTLON	
;	R37	0CLSWPT	; OVERWRITTEN TEMPORARILY BY MAGAR
;	R38	2CLSBRG	; OVERWRITTEN  BY FUELBN
;	R39	2CLSDST	; OVERWRITTEN BY FUELBN
;	R40	TEMP	COUNTER VARIABLE
;	R41	TEMP	LAT1 INPUT TO *GCLINE, INPUT TO *GCPLOT
;	R42	TEMP	LON1 INOUT TO *GCLINE, INPUT TO *GCPLOT
;	R43	TEMP	LAT2 INPUT TO *GCLINE, OUTPUT FROM *GCPLOT
;	R44	TEMP	LON2 INPUT TO *GCLINE, OUTPUT FROM *GCPLOT
;	R45	TEMP	COURSE FROM 1->2,OUTPUT FROM *GCLINE, INPUT TO *GCPLOT
;	R46	TEMP	DISTANCE FROM 1->2,OUTPUT FROM *GCLINE, INPUT TO *GCPLOT
;	R47 	TEMP  	COUNTER VARIABLE
;				
; EXTENDED MEMORY FILES
;		
; 	NAVREG	USED BY “DISPLA” FUNCTION	A043	EACH RECORD IS A LABEL 
;		FOR THE ASSOCIATED MEMORY REGISTER; A NUMERICAL PREFIX 
;		INDICATING THE TYPE AND A SIX DIGIT MNEMONIC. LABELS ARE USED 
;		ONLY WHEN DISPLAYING NAVIGATION REGISTERS.  “DISPLA” FUNCTION 
; 		LOOPS OVER FILE AND DISPLAYS EACH REGISTER CONTENTS PREFIXED 
;		WITH ITS LABEL
;
;	MAGVAR  USED BY “*MAGVAR” FUNCTION	D010	USED TO STORE POLYNOMIAL
;		COEFFICIENTS FOR INTERPOLATED MODEL OF US MAGNETIC VARIATION 
;		{ -65.6811, 0.99,0.0128899, -0.0000905928,2.87622, -0.0116268,
;		  -0.00000603925,- 0.0389806,-0.0000403488,0.000168556 }
;
;	WAYPTS	USED BY “CATWPT”,”CLSEST”, 	A008	EACH ENTRY IS A WAYPOINT
;		OR NAVIGATION FIX FOR THE FLIGHT.  ENROUT POSITION IS DISPLAYED ;		RELATIVE TO THESE FIXES. THIS FILE IS OF ARBITRARY LENGTH AND 
;		IS EDITED BY THE USER FOR EACH FLIGHT.  EACH ENTRY MUST 
;		CORRESPOND TO AN EXTENDED MEMORY FILE OF THE SAME NAME 
;		CONTAINING THE DECIMAL LAT/LON FOR THAT FIX
;
;	NAVPOS 	CREATED BY “UPDATE”, PRINTED BY “OUTPUT” A012	
;		NAVIGATION LOG FILE CONTAINING MOST RECENT POSITION, 
;		RECREATED EVERY FIVE MINUTES
;
;
;	KXXX	INDIVIDUAL WAYPOINT FILE	D002	WAYPOINT FILE 
;		CONTAINING DECIMAL LATITUDE, DECIMAL LONGITUDE 
;
;
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
 01 LBL "SLANTR" 	; MAIN ENTRY POINT FOR /R AREA NAVIGATION
 02 CLRG 		; CLEAR REGISTERS 
 03 CLRALMS 		; CLEAR ALARMS
 04 CF 00 		; FIRST BEARING RECORDED IF TRUE (VOR 1 BEARING 1)
 05 CF 01 		; UPDATE DENSITY ALTITUDE AND AIRSPEEDS
 06 SF 02 		; UPDATE TRUE AIRSPEED AND GROUND SPEED
 07 SF 03 		; USE FORECAST WINDS ALOFT FOR NAVIGATION
 08 CF 04 		; USE EXPECTED GROUND SPEED FOR NAVIGATION
 09 CF 05 		; 
 10 CF 06		;
 11 CF 07		; 1=ASSIGNED HEADING, 0=ON COURSE TO DESTINATION
 12 "TIME?" 
 13 AVIEW 
 14 PSE 
 15 CLA 
 16 FIX 04 
 17 TIME 		; PROMPT USER TO CONFIRM TIME (GMT)
 18 HMS 
 19 ATIME 
 20 AVIEW 
 21 STOP 
 22 SETIME 
 23 STO 02 		; [TIMOFF] RECORD A TIME HACK; BACKUP IF HACK MISSED
 24 XEQ 26 		; PREFLT - ENTER DEPART, DEST, ALTITUDE, TEMP
 25 XEQ 36		; INITPOS - INITIALIZE POSITIONS AT START OF FLIGHT 
 26 XEQ 86 		; UPDATE NAVIGATION
 27 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
 28 LBL "HACKTO"	; HACK TAKEOFF CREATE ALARM FOR MAIN LOOP EVERY 5 MIN 
 29 LBL 10 
 30 TIME 
 31 STO 02 		; [TIMEOF] TIME OF TAKEOFF
 32 STO 22 		; [DEDTIM] TIME OF LAST DEDUCED POSITION
 33 STO 23 		; [LASTIM] TIME OF LAST FIXED POSITION
 34 CLRALMS 
 35 .0500 		; MAIN SHOULD RUN EVERY FIVE MINUTES
 36 0 			; TODAY
 37 TIME 		;	
 38 .05 		; STARTING FIVE MINUTES FROM NOW
 39 + 			; DOES NOT HANDLE MIDNIGHT DATE ROLLOVER
 40 "^^M%" 
 41 XYZALM 
 42 RTN 
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 43 LBL "HACKOV" 
 44 LBL 12 
 45 TIME 
 46 STO 29
 47 XEC c
 48 ASTO 24 
 49 43 
 50 XEQ 99 
 51 XEQ 65 
 52 RTN 
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 53 LBL "HACKFX" 
 54 XEQ c 
 55 41 
 56 XEQ 99 
 57 XEQ b 
 58 STO 45 
 59 XEQ d 
 60 STO 46 
 61 GTO 87 
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 62 LBL "HACKC1"	; HACK TIME OF CHECKPOINT (FIRST OF PAIR), 
 63 TIME 		; COMPUTE GROUND SPEED AFTER SECOND HACK TIME
 64 STO 29 		; [TIME2] DO NOT INTERLEAVE VOR FIXES AND CHECKPOINTS
 65 RTN 
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 66 LBL "HACKC2" 	; HACK TIME OF SECOND CHECKPOINT, UPDATE GROUND SPEED
 67 TIME 
 68 RCL 29 		; [TIME2]
 69 HMS- 
 70 HR 
 71 XEQ d 
 72 X<>Y 
 73 / 
 74 STO 07 		; [GS]
 75 SF 05 		; FLAG 05 = TRUE PREVENTS GROUND SPD FROM BEING
 76 RTN 		; DERIVED FROM FORECAST WINDS
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 77 LBL "M%" 		; COMPUTE NEW DED RECKON POSITION
 78 XEQ 38 		; UPDATE DED RECON POSITION
 79 XEQ 86 		; RECOMPUTE NAVIGATION VARIABLES
 80 RTN 
 81 LBL 86 		; SUBROUTINE UPDATE NAVIGATION PARAMETERS
 82 XEQ 70 		; COMPUTE MAGNETIC VARIATION BASED ON CUR POSITION
 83 XEQ 62 		; COMPUTE DENSITY ALT BASED ON TEMPERATURE AND PRESSURE
 84 XEQ 40 		; COMPUTE COURSE,HEADING,ETE FROM CUR POS TO DEST
 85 XEQ 42 		; COMPUTE TAS FROM IAS, WIND CORRECTION , GS, HEADING
 86 XEQ 60 		; COMPUTE BEARING AND DISTANCE TO CLOSEST WAYPOINT
 87 XEQ 34 		; UPDATE NAVIGATION LOG
 88 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
; INPUT FUNCTIONS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
 89 LBL "PREFLT" 	; PROMPT USER FOR DEPARTURE, DESTINATION ALTITUDE
 90 LBL 26 		; TEMP ALOFT, BARO, AIRSPEED, WINDS ALOFT
 91 "DEP APT?" 
 92 AON 
 93 STOP 
 94 AOFF 
 95 ASTO 00 		; [DEPART]
 96 "DST APT?" 
 97 AON 
 98 STOP 
 99 AOFF 
100 ASTO 01 		; [DEST]
101 XEQ 96 
102 XEQ 97 
103 XEQ 98 
104 RTN 
105 LBL "ALTMTR" 
106 LBL 96 
107 CF 02 
108 "ENR ALTITD?" 
109 PROMPT 
110 STO 12 		; [ALTITD]
111 "TEMP?" 
112 PROMPT 
113 STO 10 		; [TEMPC]
114 "ALT SET?" 
115 PROMPT 
116 STO 11 		; [BARO]
117 RTN 
118 LBL 97 
119 "IAS?" 
120 PROMPT 
121 STO 09 		; [IAS]
122 RTN 
123 LBL "FCSTWD" 
124 LBL 98 
125 "WIND DIR?" 
126 PROMPT 
127 STO 14 		; [WINDIR]
128 "WIND SPD?" 
129 PROMPT 
130 STO 15 		; [WINSPD]
131 RTN 
;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;
132 LBL "HEADNG"
133 XEQ 38
134 XEQ e
135 STO 04
136 SF 07
137 RTN
138 LBL "RESUME"
139 CF 05
140 XEQ 38
141 XEQ 40
142 XEQ 42
143 RTN
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
144 LBL "CATWPT" 	; CATALOG ALL WAYPOINTS IN EXTENDED MEMORY
145 FIX 04 
146 CLX 
147 STO 40 
148 CF 25 
149 LBL 31 
150 "WAYPTS" 		; WAYPOINTS FILE LISTS INDIVIDUAL ENTRIES
151 RCL 40 
152 SEEKPTA 
153 RDN 
154 SF 25 
155 GETREC 		; RETRIEVE WAYPOINT NAME
156 AVIEW 
157 FC? 25 
158 GTO 33 
159 41 
160 XEQ 99 
161 RCL 41 
162 HMS 
163 CLA 
164 ATIME24 
165 RCL 42 
166 >"," 
167 ATIME24 
168 AVIEW 
169 PSE 
170 ISG 40 
171 LBL 09 
172 GTO 31 
173 LBL 33 
174 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
175 LBL "NAVLOG" 
176 LBL 34 
177 FIX 00 
178 "TIMEGMT=" 
179 FIX 02 
180 TIME 
181 ATIME24 
182 AVIEW 
183 PSE 
184 RCL 05 
185 RCL 07 
186 / 
187 FIX 02 
188 HMS 
189 CLA 
190 ATIME 
191 ATOX 
192 ASTO 41 
193 "C" 
194 RCL 03 
195 XEQ 35 
196 FS? 07 
197 >"A" 
198 FC? 07 
199 >"H" 
200 RCL 04 
201 XEQ 35 
202 >"E" 
203 ARCL 41 
204 AVIEW 
205 PSE 
206 "I" 
207 RCL 09 
208 XEQ 35 
209 >"T" 
210 RCL 08 
211 XEQ 35 
212 >"G" 
213 RCL 07 
214 XEQ 35 
215 AVIEW 
216 PSE 
217 "FLN" 
218 RCL 06 
219 XEQ 35 
220 >"RMG" 
221 RCL 05 
222 XEQ 35 
223 AVIEW 
224 PSE 
225 CLA 
226 ARCL 37 
227 RCL 39 
228 XEQ 35 
229 >"T" 
230 RCL 38 
231 XEQ 35 
232 AVIEW 
233 PSE 
234 RTN 
235 LBL 35 
236 FIX 00 
237 99 
238 X<>Y 
239 X>Y? 
240 GTO 85 
241 >"0" 
242 9 
243 X<>Y 
244 X>Y? 
245 GTO 85 
246 >"0" 
247 LBL 85 
248 ARCL X 
249 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
250 LBL "DISPLA" 	; FORMATTED DISPLAY OF NAVIGATION REGISTERS
251 .03901 
252 STO 40 
253 LBL 46 
254 RCL 40 
255 INT 
256 XEQ 47 
257 ISG 40 
258 GTO 46 
259 RTN 
260 LBL 47 		; HELPER FUNCTION FOR FORMATTED DISPLAY, REG NUM IN X
261 "NAVREG" 
262 SEEKPTA 		; LOOKUP REGISTER TYPE AND NAME IN EXT MEMORY FILE
263 GETREC 
264 >":" 
265 RCL IND X 
266 ATOX 
267 ASTO Z 
268 X<>Y 
269 FIX 00 
270 XEQ IND Y 
271 AVIEW 
272 RTN 
273 LBL 48 		; ALPHA
274 ARCL X 
275 RTN 
276 LBL 49 		; KTS
277 ARCL X 
278 >"KTS" 
279 RTN 
280 LBL 50 		; NM
281 ARCL X 
282 >" NM" 
283 RTN 
284 LBL 51 		; CELCIUS
285 LBL 52 		; DEGREES
286 LBL 56 		; BAROMETRIC PRESSURE
287 LBL 57 		; FT
288 ARCL X 
289 RTN 
290 LBL 53 		; HMS
291 CLK24 
292 FIX 04 
293 ATIME 
294 RTN 
295 LBL 54 
296 LBL 55 		; LAT LONG
297 FIX 04 
298 HMS 
299 ATIME24 
300 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; NAVIGATION FUNCTIONS
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; 
301 LBL "*INIPOS" 	; LOOKUP DEP LAT/LOT, LOOKUP DST LAT/LON, 
302 LBL 36 	  	; INITIALIZE FIX AND DED POSITIONS TO DEPARTURE
303 CLA 
304 ARCL 00 		; DEPART
305 33 			; DEPLAT.DEPLON
306 XEQ 99 		; LOOKUP DEPARTURE WAYPOINT
307 CLA 
308 ARCL 01 		; DEST
309 35 			; DSTLAT.DSTLON
310 XEQ 99
311 RCL 33 		; DEPLAT
312 STO 17 		; FIXLAT  SET LAST “FIX” TO DEPARTURE LATITUDE
313 STO 20 		; DEDLAT  SET LAST “DEDUCED” POS TO DEPART LAT
314 RCL 34		; DEPLON 
315 STO 18 		; FIXLON  SET LAST “FIX” TO DEPARTURE LONGITUDE
316 STO 21 		; DEDLON  SET LAST “DEDUCED” POS TO DEPARTURE LONG
317 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
318 LBL "*CRSHDG" 	; COMPUTE TRUE COURSE AND MAGNETIC HEADING,
319 LBL 40 		; DISTANCE REMAINING AND DISTANCE FLOWN
320 RCL 20 		; [DEDLAT]      DEDUCED LAT
321 STO 41 
322 RCL 21 		; [DEDLON]      DEDUCED LON
323 STO 42 
324 RCL 35 		; [DSTLAT]      DESTINATION LAT
325 STO 43 
326 RCL 36 		; [DSTLON]      DESTINATION LON
327 STO 44 
328 XEQ 74 		; *GCLINE
329 RCL 45 
330 FC? 07 
330 STO 03 		; TRUCRS        UPDATE TRUE COURSE
330 RCL 46 
331 STO 05 		; [DISTRM]      UPDATE DISTANCE REMAINING
332 RCL 33 		; [DEPLAT]      DEPARTURE LATITUDE
333 STO 43 
334 RCL 34 		; [DEPLON]      DEPARTURE LONGITUDE
335 STO 44 
338 XEQ 74 		; *GCLINE
339 RCL 46 
340 STO 06 		; [DISTFN]      UPDATE DISTANCE FLOWN
341 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
342 LBL "*GCCRS" 	; COMPUTE COURSE AND DISTANCE BETWEEN TWO LAT/LON PAIRS
343 LBL 74 		; FROM HP “NAVIGATION” APPLICATION PAC
344 RCL 44 
345 RCL 42 
346 - 
347 RCL 41 
348 RCL 43 
349 XEQ 78 		; *SIGHT
350 90 
351 - 
352 CHS 
353 60 
354 * 
355 X<>Y 
356 360 
357 MOD 
358 X<>Y 
359 FIX 05 
360 RND 
361 STO 46 
362 X<>Y 
363 STO 45 
364 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; *SIGHT ROUTINE IS COLLAPSED TO SIMPLY CALL XROM FUNCTION IN NAVIGATION PACK
;; 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
365 LBL 78
366 CLA
367 ARCL 00
368 XROM "*SRT" ; XROM 14,23
369 ASTO 00
370 RTN
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;; LBL "*SIGHT" ; ADAPTED FROM HP “NAVIGATION” APPLICATION PAC
;;; LBL 78 
;;; 1 
;;; P-R 
;;; R^ 
;;; X<>Y 
;;; P-R 
;;; R^ 
;;; STO 40 
;;; X<>Y 
;;; P-R 
;;; X<> 40 
;;; R^ 
;;; P-R 
;;; X<> 40 
;;; + 
;;; X<> 40 
;;; - 
;;; R-P 
;;; RDN 
;;; 180 
;;; + 
;;; RCL 40 
;;; ASIN 
;;; RTN 
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
371 LBL "*GCPLT" 	; COMPUTE COURSE, BEARING BETWEEN TWO LAT/LON POSITIONS
372 LBL 76 	  	; ADAPTED FROM HP “NAVIGATION” APPLICATION PAC
373 RCL 45 		; <HEADING>
374 90 
375 RCL 46 		; <DISTANCE>
376 60 
377 / 
378 - 
379 RCL 41 		; Latitude 1
380 X<>Y 
381 XEQ 78 
382 STO 43 		; RESULTING POSITION (LATITUDE)
383 X<>Y 
384 RCL 42 		; Longitude 1
385 + 
386 1 
387 P-R 
388 R-P 
389 X<>Y 
390 FIX 05 
391 RND 
392 STO 44 		; RESULTING POSITION (LONGITUDE)
393 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
394 LBL "*DEDREC" 
395 LBL 38 
396 TIME 		; HACK TIME
397 STO 40 
398 RCL 22 		; [DEDTIM] TIME SINCE LAST UPDATED DED RECON POSITION
399 HMS- 
400 HR 			; CONVERT TO DECIMAL HOURS
401 RCL 07 		; [GNDSPD]  EXPECTED GROUND SPEED NM/HR
402 * 
403 RND 
404 STO 46 		; DISTANCE FLOWN
405 RCL 20 		; [DEDLAT]
406 STO 41 		; LAST DEDUCED LATITUDE
407 RCL 21 		; [DEDLON]
408 STO 42 		; LAST DEDUCED LONGITUDE
409 RCL 03 		; [TRUCRS]
410 STO 45 		; ASSUME TRACKING TRUE COURSE
411 RCL 40 
412 STO 22 		; [DEDTIM]
413 XEQ 76 		; *GCPLOT
414 RCL 43 
415 STO 20 
416 RCL 44 
417 STO 21 
418 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
419 LBL "VOR1B1" 	; HACK TIME OF SECOND BEARING, PROMPT FOR BEARING FROM
420 TIME 		; ADAPTED FROM HP “AVIATION” APPLICATION PAC
421 STO 28 		; [TIME1]
422 STO 29 		; [TIME2]
423 XEQ c		; PROMPT FOR "VOR ID?" 
424 ASTO 25 		; VOR1  -- NO ERROR CHECKING FOR NON-EXISTENT WAYPOINTS
425 XEQ b		; PROMPT FOR "BEARING" 
426 STO 26 		; [BEARING1]
427 SF 01 		; MARK FIRST BEARING
428 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
429 LBL "VOR1B2" ; HACK TIME OF SECOND BEARING, PROMPT FOR BEARING FROM
430 TIME         ; ADAPTED FROM HP “AVIATION” APPLICATION PAC
431 STO 29 	 ; [TIME2]
432 FC? 01 	 ; HAS FIRST BEARING BEEN TAKEN
433 GTO 80 
434 XEQ b 	 ; PROMPT FOR BEARING FROM (SCRIMPING ON REGISTERS)
435 STO 45 
436 XEQ a 	 ; CALCULATE RECIPROCAL (TO) FOR FORMULA
437 STO 27       ; [BRNG2]
438 RCL 29 	 ; [TIME2] DIST = (GS * T) * SIN (COURSE - R1) / SIN (R1 - R2)
439 RCL 28 	 ; [TIME1]
440 HMS- 
441 HR 
442 RCL 07       ; [GNDSPD]
443 * 
444 RCL 03 	 ; [TRUCRS]  MAGNETIC COURSE
445 RCL 04       ; [BEARING1]
446 + 
447 RCL 26 
448 XEQ a        ; CALCULATE RECIPROCAL (TO) FOR FORMULA
449 - 
450 SIN 
451 * 
452 RCL 26       ; [BEARING1]
453 RCL 27       ; CALCULATE RECIPROCAL (TO) FOR FORMULA
454 - 		 ; [BEARING2]
455 SIN 
456 / 
457 ABS 
458 STO 46       ; DISTANCE FROM VOR
459 "D=" 
460 ARCL X 
461 AVIEW 
462 CLA 
463 ARCL 25      ; MARK VOR AS LAST FIX
464 ASTO 24 	 ; [LSTFIX]
465 41 
466 XEQ 99      ; RETRIEVE VOR LAT/LON
467 GTO 97
468 LBL 80 
469 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
470 LBL "VOR2B1" 	; ADAPTED FROM HP “AVIATION” APPLICATION PAC
471 XEQ c		; PROMPT FOR VOR ID 
472 ASTO 30 		; [VOR2]
473 43 
474 XEQ 99 		; LAT2, LON2 inputs to *GCLINE
475 XEQ b 		; PROMPT FOR BEARING FROM (SCRIMPING ON REGISTERS)
476 STO 31 		; [BRNG3]
477 CLA 
478 ARCL 25 		; [VOR2]
479 41 
480 XEQ 99 
481 XEQ 74 		;  *GCLINE
482 RCL 31 		; [BRNG3]
483 RCL 45 		; COURSE FROM 1->2
484 - 
485 SIN 
486 RCL 46 		; DISTANCE FROM 1->2
487 * 
488 RCL 31 		; [BRNG3]
489 RCL 26 		; [BRNG1]
490 STO 45 		; BEARING FROM VOR2
491 - 
492 SIN 
493 / 
494 ABS 
495 STO 46 		; DISTANCE FROM VOR1
496 "D=" 		; TEMPORARY DEBUGGING OUTPUT!
497 ARCL X 
498 AVIEW 
499 RCL 26 		; [BRNG1]       ; BEARING FROM VOR1
500 STO 45 
501 LBL 87
502 XEQ 76 		; *GCPLOT       ;
503 XEQ 65 
504 XEQ 86 
505 RTN 
506 LBL 65 
507 RCL 43 
508 STO 17 		; [FIXLAT]
509 STO 20 		; [DEDLAT]
510 RCL 44 
511 STO 18 		; [FIXLON]
512 STO 21 		; [DEDLON]
513 RCL 29 		; [TIME2]
514 STO 22 		; [DEDTIM]
515 STO 23 		; [LASTIM]
516 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
517 LBL a 		; COMPUTE RECIPROCAL BEARING
518 180 
519 + 
520 360 
521 MOD 
522 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
523 LBL b 
524 "BRNG FROM?" 
525 PROMPT 
526 RTN 
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
527 LBL c
528 "VOR ID?"
529 AON
530 STOP
531 AOFF
532 RTN
;;      
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
533 LBL d
534 "DIST?"
535 PROMPT
536 RTN
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
537 LBL e
538 "MAGHD?"
539 PROMPT
540 RTN
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
541 LBL 99
542 ENTER
543 ENTER
544 1
545 +
546 1 E3
547 /
548 +
549 0
550 SEEKPTA
551 RDN
552 GETRX
553 RTN
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
554 LBL "*CLSEST" 	; CLOSEST WAYPOINT IN DATABASE, VOR OR AIRPORT
555 LBL 60 
556 CLD
557 0
558 STO 47 
559 1 E3 		; INITIALIZE CLOSEST DISTANCE TO 1000 NM
560 STO 39 		; CLSDST  CURRENT LAT/LON FROM DEDUCED POSITION
561 RCL 20 		; DEDLAT
562 STO 41 
563 RCL 21 		; DEDLON
564 STO 42 
565 CF 25 
566 LBL 61 
567 "WAYPTS" 
568 RCL 47 
569 SEEKPTA 
570 RDN 
571 SF 25 
572 LBL 82 
573 GETREC 
574 FC? 25 
575 GTO 84 
576 ASTO 30 		;  USE VOR 2 AS TEMPORARY VARIABLE
577 43
578 XEQ 99 
589 XEQ 74 		; *GCLINE
590 RCL 39 		; CLSDST CURRENT DISTANCE TO CLOSEST WAYPOINT
591 RCL 46 		; DISTANCE TO WAYPOINT BEING TESTED
592 X>Y? 
593 GTO 83 
594 STO 39 		; CLSDST
595 RCL 45 
596 STO 38 		; CLSBRG
597 CLA 
598 ARCL 30 
599 ASTO 37 		; CLSWPT
592 LBL 83 
593 1 
594 ST+ 47 
595 GTO 61 
596 LBL 84 
597 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
; INPUTS DED RECON LAT (N) =R19
; DED RECON LONG (W) = R20
; NOTE OVERWRITES CLOSEST WAYPOINT BEARING AND DISTANCE
; FROM ED WILLIAMS (REPRODUCED WITHOUT PERMISSION)
; -65.6811
; x     ;CONUS
; x=latitude (N deg) y=longitude (W deg) var= variation 
; var=-65.6811+0.99*x+0.0128899*x^2-0.0000905928*x^3+2.87622*y-
; 0.0116268*x*y - 0.00000603925*x^2*y - 0.0389806*y^2 -
; 0.0000403488*x*y^2 + 0.000168556*y^3
; 0.0128899
; (24 < x < 50,  66 < y < 125)
;
596 LBL "*MAGVAR" 
597 LBL 70 
598 "MAGVAR" 
599 0 
600 SEEKPTA 
601 38.047 
602 GETRX 
603 RCL 20 
604 ST* 39 
605 ST* 41 
606 ST* 43 
607 ST* 46 
608 X^2 
609 ST* 40 
610 ST* 41 
611 RCL 21 
612 ST* 42 
613 ST* 43 
614 ST* 44 
615 ST* 47 
616 X^2 
617 ST* 45 
618 ST* 46 
619 ST* 47 
620 39.047001 
621 STO 13 
622 LBL 66 
623 RCL IND 13 
624 ST+ 38 
625 ISG 13 
626 GTO 66 
627 RCL 38 
628 STO 13 
629 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
630 LBL "*DENALT" 
631 LBL 62 
632 FS? 02 
633 GTO 63 
634 RCL 12 
635 STO 16 
636 FIX 02 
637 RCL 11 
638 29.92126 
639 / 
640 .190261 
641 Y^X 
642 CHS 
643 1 
644 + 
645 145442.2 
646 * 
647 RCL 12 
648 + 
649 STO 40 
650 RCL 10 
651 273.15 
652 + 
653 STO 41 
654 288.15 
655 .0019812 
656 RCL 09 
657 * 
658 - 
659 STO 42 
660 RCL 41 
661 / 
662 .0234969 
663 Y^X 
664 CHS 
665 1 
666 + 
667 RCL 42 
668 * 
669 .0019812 
670 / 
671 RCL 40 
672 + 
673 STO 16 
674 SF 02 
675 LBL 63 
676 RTN 
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
677 LBL "*SPDHDG" 
678 LBL 42 
679 FIX 00 
680 RCL 16 
681 6.8755856 E-6 
682 * 
683 CHS 
684 1 
685 + 
686 2.127940 
687 Y^X 
688 1/X 
689 RCL 09 
690 * 
691 STO 08 
692 FC? 07 
693 GTO 95 
694 RCL 04 
695 RCL 13 
696 + 
697 GTO 94 
698 LBL 95 
699 RCL 03 
700 LBL 94 
701 RCL 08 
702 P-R 
703 RCL 14 
704 XEQ a 
705 RCL 15 
706 P-R 
707 X<>Y 
708 RDN 
709 + 
710 RDN 
711 + 
712 R^ 
713 R-P 
714 STO 07 
715 RDN 
716 FS? 07 
717 GTO 02 
718 RCL 03 
719 - 
720 CHS 
721 RCL 03 
722 + 
723 RCL 13 
724 + 
725 360 
726 MOD 
727 STO 04 
728 LBL 92 
729 RTN 
730 LBL 02 
731 360 
732 MOD 
733 STO 03 
734 RTN 
735 LBL "ACTWND" 
736 XEQ e 
737 RCL 13 
738 - 
739 RCL 08 
740 P-R 
741 "CRS MADE?" 
742 PROMPT 
743 RCL 07 
744 P-R 
745 X<>Y 
746 RDN 
747 - 
748 RDN 
749 - 
750 R^ 
751 R-P 
752 STO 15 
753 RDN 
754 360 
755 MOD 
756 STO 14 
757 RTN 
758 LBL "ENDFLT" 
759 CLRALMS 
760 20140721 
761 END 

